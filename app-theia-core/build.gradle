apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion 22
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    dataBinding {
        enabled = true
    }

    //inject api keys
    try {
        defaultConfig.buildConfigField("String", "TMDB_API_KEY", String.format("\"%s\"", (String) TMDB_API_KEY))
        defaultConfig.buildConfigField("String", "TVDB_API_KEY", String.format("\"%s\"", (String) TVDB_API_KEY))
    } catch (MissingPropertyException e) {
        printf("Api keys not configured. Did you setup the gradle.properties file? \n%s", e)
        defaultConfig.buildConfigField("String", "TMDB_API_KEY", "\"NOKEY\"")
        defaultConfig.buildConfigField("String", "TVDB_API_KEY", "\"NOKEY\"")
    }
}

dependencies {
    kapt "com.google.guava:guava:$kapt_guavaVersion"
    kapt "com.google.dagger:dagger-compiler:$dagger2Version"
    kapt "com.google.dagger:dagger-android-processor:$dagger2Version"
    kapt "com.android.databinding:compiler:$gradleToolsVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"
    kapt "android.arch.lifecycle:compiler:$androidarchVersion"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation "com.android.support:support-annotations:$supportLibVersion"

    implementation "android.arch.lifecycle:extensions:$androidarchVersion"

    //for tv/movie db api
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"

    implementation "com.github.bumptech.glide:glide:$glideVersion"
    implementation "com.github.bumptech.glide:okhttp3-integration:$glideVersion@jar"

    implementation project(':dagger2')
    implementation project(':reactivex2')
    implementation project(':media')
    implementation project(':media-loader-cds')
    implementation project(':media-loader-doc')
    implementation project(':media-loader-storage')
    implementation project(':media-playback')
    implementation project(':media-database')
    implementation project(':libtvdb2')
    implementation project(':libtmdb')
    implementation project(':logging')
    implementation project(':upnp-cds-browser')
    implementation project(':upnp-cds-featurelist')

    testImplementation project(':media-testdata')
    testImplementation "junit:junit:$test_junitVersion"
    testImplementation "org.robolectric:robolectric:$test_robolectricVersion"
    //testImplementation 'com.squareup.okhttp:okhttp-testing-support:2.7.5'
    testImplementation "com.squareup.okhttp3:mockwebserver:$test_mockwebserverVersion"
    testImplementation "org.assertj:assertj-core:$test_assertJVersion"
    testImplementation "org.apache.commons:commons-lang3:$commonsLangVersion"
    testImplementation "com.nhaarman:mockito-kotlin:$test_mockitokotlinVersion"

}

configurations.all {
    // check for updates every build (to avoid this use offline mode)
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}
